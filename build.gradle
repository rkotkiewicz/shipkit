// First, apply the publishing plugin
buildscript {
    repositories {
        mavenLocal()        // for downstream test
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'org.shipkit:shipkit:2.1.8'
        classpath 'com.gradle:build-scan-plugin:1.16'
        classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.4.6'
    }
}

apply plugin: 'com.gradle.build-scan'

buildScan {
    publishAlways()
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

apply from: "$rootDir/gradle/java8-compatibility.gradle"
apply from: "$rootDir/gradle/precommit.gradle"

apply plugin: 'base'
apply plugin: 'org.shipkit.gradle-plugin'

allprojects {
    group = 'org.shipkit'

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    tasks.withType(Test) {
        minHeapSize = '128m'
        maxHeapSize = '512m'
        testLogging {
            //This way Spock/Groovy power asserts will be visible in the build log
            //Very useful - you don't have to look at the test results to debug a test failure
            exceptionFormat "full"
        }
    }
}

